plugins {
    id 'idea'
    id 'application'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.6.1'
}

group 'com.github.sukhinin'
description 'Prometheus remote read backend with ClickHouse import'

repositories {
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'net.sourceforge.argparse4j:argparse4j:0.8.1'
    implementation 'com.github.sukhinin:simple-config:1.3.0'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'io.javalin:javalin:3.6.0'
    implementation 'ru.yandex.clickhouse:clickhouse-jdbc:0.2.1'
    implementation 'org.xerial.snappy:snappy-java:1.1.7.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.2'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-protobuf:2.10.2'
    implementation 'io.micrometer:micrometer-core:1.3.1'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.3.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = 'com.github.sukhinin.prometheus.clickhouse.reader.RemoteReadServer'
}

jgitver {
    useDirty true
    useGitCommitID true
}

publishing {
    publications {
        maven(MavenPublication) { publication ->
            artifact(shadowJar)
            artifact(kotlinSourcesJar)
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/sukhinin/prometheus-clickhouse-reader'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://github.com/sukhinin/prometheus-clickhouse-reader/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sukhinin'
                        name = 'Boris Sukhinin'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/sukhinin/prometheus-clickhouse-reader.git'
                    developerConnection = 'scm:git:https://github.com/sukhinin/prometheus-clickhouse-reader.git'
                    url = 'https://github.com/sukhinin/prometheus-clickhouse-reader'
                }
            }
        }
    }
}

bintray {
    user = project.findProperty('bintray.user') ?: System.getenv('BINTRAY_USER')
    key = project.findProperty('bintray.apikey') ?: System.getenv('BINTRAY_APIKEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/sukhinin/prometheus-clickhouse-reader'
        issueTrackerUrl = 'https://github.com/sukhinin/prometheus-clickhouse-reader/issues'
        vcsUrl = 'https://github.com/sukhinin/prometheus-clickhouse-reader.git'
        licenses = ['Apache-2.0']
        labels = ['prometheus', 'clickhouse', 'remote', 'read']
        githubRepo = 'sukhinin/prometheus-clickhouse-reader'
    }
}
